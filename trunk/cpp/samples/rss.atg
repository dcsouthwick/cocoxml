/*-------------------------------------------------------------------------
rss.atg -- RSS(Really Simple Syndication) Grammer
Copyright (c) 2008 Charles Wang <charlesw123456@gmail.com>

This program is free software; you can redistribute it and/or modify it 
under the terms of the GNU General Public License as published by the 
Free Software Foundation; either version 2, or (at your option) any 
later version.

This program is distributed in the hope that it will be useful, but 
WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY 
or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License 
for more details.

You should have received a copy of the GNU General Public License along 
with this program; if not, write to the Free Software Foundation, Inc., 
59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
-------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------
 compile with:
   CocoXml rss.atg
-------------------------------------------------------------------------*/

COMPILER Rss

    Rss rss;

/*-------------------------------------------------------------------------*/
OPTIONS TEXT

TAGS
    RSS = "rss"
    CHANNEL = "channel"
    ITEM = "item"
    TITLE = "title"
    LINK = "link"
    DESCRIPTION = "description"
    LANGUAGE = "language"
    COPYRIGHT = "copyright"
    MANAGING_EDITOR = "managingEditor"
    WEB_MASTER = "webMaster"
    PUB_DATE = "pubDate"
    LAST_BUILD_DATE = "lastBuildDate"
    CATEGORY = "category"
    GENERATOR = "generator"
    DOCS = "docs"
    CLOUD = "cloud"
    TTL = "ttl"
    IMAGE = "image"
    RATING = "rating"
    TEXT_INPUT = "textInput"
    NAME = "name"
    SKIP_HOURS = "skipHours"
    SKIP_DAYS = "skipDays"
    URL = "url"
    WIDTH = "width"
    HEIGHT = "height"
    AUTHOR = "author"
    COMMENTS = "comments"
    ENCLOSURE = "enclosure"
    GUID = "guid"
    SOURCE = "source"

ATTRS
    ATTR_DOMAIN = "domain"
    ATTR_PORT = "port"
    ATTR_PATH = "path"
    ATTR_REGISTER_PROCEDURE = "registerProcedure"
    ATTR_PROTOCOL = "protocol"

/*-------------------------------------------------------------------------*/
PRODUCTIONS

Rss = (. Channel channel; rss = new Rss(); .)
    RSS {
    Channel<.channel.> (. rss.AddChannel(channel); .)
    } END_RSS.

Channel<.Channel channel.> = (. channel = new Channel(); .)
    CHANNEL { ChannelProperty<channel> } END_CHANNEL.

ChannelProperty<Channel channel>  (. string value;
                         Cloud cloud;
                     Image image;
                     TextInput textInput;
                     Item item; .) =
    Title<.value.>            (. channel.title = value; .)
    | Link<.value.>           (. channel.link = value; .)
    | Description<.value.>    (. channel.description = value; .)
    | Language<.value.>       (. channel.language = value; .)
    | Copyright<.value.>      (. channel.copyright = value; .)
    | ManagingEditor<.value.> (. channel.managingEditor = value; .)
    | WebMaster<.value.>      (. channel.webMaster = value; .)
    | PubDate<.value.>        (. channel.pubDate = value; .)
    | LastBuildDate<.value.>  (. channel.lastBuildDate = value; .)
    | Category<.value.>       (. channel.category = value; .)
    | Generator<.value.>      (. channel.generator = value; .)
    | Docs<.value.>           (. channel.docs = value; .)
    | Cloud<.cloud.>          (. channel.cloud = cloud; .)
    | Ttl<.value.>            (. channel.ttl = value; .)
    | Image<.image.>          (. channel.image = image; .)
    | Rating<.value.>         (. channel.rating = value; .)
    | TextInput<.textInput.>  (. channel.textInput = textInput; .)
    | SkipHours<.value.>      (. channel.skipHours = value; .)
    | SkipDays<.value.>       (. channel.skipDays = value; .)
    | Item<.item.>            (. channel.AddItem(item); .)
.

Cloud<.Cloud cloud.> = (. cloud = new Cloud(); .)
    CLOUD
    { ATTR_DOMAIN              (. cloud.domain = t.val; .)
    | ATTR_PORT                (. cloud.port = t.val; .)
    | ATTR_PATH                (. cloud.path = t.val; .)
    | ATTR_REGISTER_PROCEDURE  (. cloud.registerProcedure = t.val; .)
    | ATTR_PROTOCOL            (. cloud.protocol = t.val; .)
    } END_CLOUD
.

Image<.Image image.> = (. image = new Image(); .)
    IMAGE { ImageProperty<image> } END_IMAGE.

ImageProperty<Image image>  (. string value; .) =
    Url<.value.>      (. image.url = value; .)
    | Title<.value.>  (. image.title = value; .)
    | Link<.value.>   (. image.link = value; .)
    | Width<.value.>  (. image.width = value; .)
    | Height<.value.> (. image.height = value; .)
.

TextInput<.TextInput textInput.> = (. textInput = new TextInput(); .)
    TEXT_INPUT { TextInputProperty<textInput> } END_TEXT_INPUT.

TextInputProperty<TextInput textInput> (. string value; .) =
    Title<.value.>         (. textInput.title = value; .)
    | Description<.value.> (. textInput.description = value; .)
    | Name<.value.>        (. textInput.name = value; .)
    | Link<.value.>        (. textInput.link = value; .)
.

Item<.Item item.> = (. item = new Item(); .)
    ITEM { ItemProperty<item> } END_ITEM.

ItemProperty<Item item> (. string value; Image image; .) =
    Title<.value.>          (. item.title = value; .)
    | Link<.value.>         (. item.link = value; .)
    | Description<.value.>  (. item.description = value; .)
    | Author<.value.>       (. item.author = value; .)
    | Category<.value.>     (. item.category = value; .)
    | Comments<.value.>     (. item.comments = value; .)
    | Enclosure<.value.>    (. item.enclosure = value; .)
    | Guid<.value.>         (. item.guid = value; .)
    | PubDate<.value.>      (. item.pubdate = value; .)
    | Source<.value.>       (. item.source = value; .)
    | Image<.image.>        (. item.image = image; .)
.


Title<.string value.> = TITLE TEXT (. value = t.val; .) END_TITLE.

Link<.string value.> = LINK TEXT (. value = t.val; .) END_LINK .

Description<.string value.> = DESCRIPTION TEXT (. value = t.val; .) END_DESCRIPTION .

Language<.string value.> = LANGUAGE TEXT (. value = t.val; .) END_LANGUAGE.

Copyright<.string value.> = COPYRIGHT TEXT (. value = t.val; .) END_COPYRIGHT.

ManagingEditor<.string value.> = MANAGING_EDITOR TEXT (. value = t.val; .) END_MANAGING_EDITOR.

WebMaster<.string value.> = WEB_MASTER TEXT (. value = t.val; .) END_WEB_MASTER.

PubDate<.string value.> = PUB_DATE TEXT (. value = t.val; .) END_PUB_DATE.

LastBuildDate<.string value.> = LAST_BUILD_DATE TEXT (. value = t.val; .) END_LAST_BUILD_DATE.

Category<.string value.> = CATEGORY TEXT (. value = t.val; .) END_CATEGORY.

Generator<.string value.> = GENERATOR TEXT (. value = t.val; .) END_GENERATOR.

Docs<.string value.> = DOCS TEXT (. value = t.val; .) END_DOCS.

Ttl<.string value.> = TTL TEXT (. value = t.val; .) END_TTL.

Url<.string value.> = URL TEXT (. value = t.val; .) END_URL.

Width<.string value.> = WIDTH TEXT (. value = t.val; .) END_WIDTH.

Height<.string value.> = HEIGHT TEXT (. value = t.val; .) END_HEIGHT.

Rating<.string value.> = RATING TEXT (. value = t.val; .) END_RATING.

Name<.string value.> = NAME TEXT (. value = t.val; .) END_NAME.

SkipHours<.string value.> = SKIP_HOURS TEXT (. value = t.val; .) END_SKIP_HOURS.

SkipDays<.string value.> = SKIP_DAYS TEXT (. value = t.val; .) END_SKIP_DAYS.

Author<.string value.> = AUTHOR TEXT (. value = t.val; .) END_AUTHOR.

Comments<.string value.> = COMMENTS TEXT (. value = t.val; .) END_COMMENTS.

Enclosure<.string value.> = ENCLOSURE TEXT (. value = t.val; .) END_ENCLOSURE.

Guid<.string value.> = GUID TEXT (. value = t.val; .) END_GUID.

Source<.string value.> = SOURCE TEXT (. value = t.val; .) END_SOURCE.

END Rss.
